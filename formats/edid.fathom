//! # Extended Display Identification Data
//!
//! ## References
//!
//! - https://en.wikipedia.org/wiki/Extended_Display_Identification_Data

//~ example-data = [
//~   "data/edid/*.edid",
//~ ]
//~
//~ example-data-invalid = [
//~   "data/edid/invalid/*.edid",
//~ ]

// TODO: Versions 1.0-1.4

def header = {
    magic <- u64le where u64_eq magic 0x00ffffffffffff00,
    manufacturer_id <- u16le,                // TODO: bit patterns
    product_code <- u16le,
    serial <- u32le,
    manufacturer_week <- u8,
    manufacturer_year_mod <- u8,             // TODO: `mfg_year_mod + 1990`
    edid_version_major <- u8,
    edid_version_minor <- u8,
};

def display_parameters = {
    video_input_parameters <- u8,            // TODO: bit patterns
    screen_size_h <- u8,
    screen_size_v <- u8,
    gamma_mod <- u8,                         // TODO: `(int_to_f32 gamma_mod + 100.0) / 100.0`
    supported_features <- u8,                // TODO: bit patterns
};

def chromacity_coordinates = {
    red_green_lsb <- u8,                     // TODO: bit patterns
    blue_white_lsb <- u8,                    // TODO: bit patterns
    red_x_msb <- u8,
    red_y_msb <- u8,
    green_x_msb <- u8,
    green_y_msb <- u8,
    blue_x_msb <- u8,
    blue_y_msb <- u8,
    white_x_msb <- u8,
    white_y_msb <- u8,
};

def established_timing = {
    mode_bitmap <- repeat_len8 3 u8,         // TODO: bit patterns
};

def standard_timing_information : Format = {
    // TODO: Standard timing information
};

def main = {
    header <- header,
    display_parameters <- display_parameters,
    chromacity_coordinates <- chromacity_coordinates,
    established_timing <- established_timing,
    standard_timing_information <- standard_timing_information,
};
